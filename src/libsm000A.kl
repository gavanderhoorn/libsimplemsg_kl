
-- Copyright (c) 2016, TU Delft Robotics Institute
--
-- Licensed under the Apache License, Version 2.0 (the "License");
-- you may not use this file except in compliance with the License.
-- You may obtain a copy of the License at
--
--     http://www.apache.org/licenses/LICENSE-2.0
--
-- Unless required by applicable law or agreed to in writing, software
-- distributed under the License is distributed on an "AS IS" BASIS,
-- WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
-- See the License for the specific language governing permissions and
-- limitations under the License.


PROGRAM libsm000A
--------------------------------------------------------------------------------
--
-- ROS-Industrial Simple Message Joint Position (libsm000A) implementation
--
-- author: G.A. vd. Hoorn (TU Delft Robotics Institute)
--
--------------------------------------------------------------------------------
%ALPHABETIZE
%COMMENT = 'ROS//r0'
%NOBUSYLAMP
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR




--------------------------------------------------------------------------------
--
-- remote types & constants
--
--------------------------------------------------------------------------------
%INCLUDE stdex\ktype_sz.th
%INCLUDE simplemsg\simplemsg.th
%INCLUDE simplemsg\sm_hdr.th




--------------------------------------------------------------------------------
--
-- local types & constants
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\msgs\sm000A.th




--------------------------------------------------------------------------------
--
-- local variables
--
--------------------------------------------------------------------------------
VAR
	msg_len_     : INTEGER




--------------------------------------------------------------------------------
--
-- remote routine prototypes
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\sm_hdr.h




--------------------------------------------------------------------------------
--
-- local routine prototypes
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\msgs\sm000A.h




--------------------------------------------------------------------------------
--
-- Empty body
--
--------------------------------------------------------------------------------
BEGIN
END libsm000A




ROUTINE sm000A_len
BEGIN
	IF (UNINIT(msg_len_)) THEN
		msg_len_ = ( &
			-- sequence nr
			(1 * SZ_INT) + &
			-- joint data
			(10 * SZ_REAL))
	ENDIF
	RETURN (msg_len_)
END sm000A_len




ROUTINE sm000A_ctor
BEGIN
	sm000A_rset(this)
END sm000A_ctor




ROUTINE sm000A_rset
VAR
	i__          : INTEGER
BEGIN
	sm_hdr_rset(this.hdr)
	this.hdr.msg_type   = SM000A_ID
	this.hdr.comm_type  = SM_CT_TOPIC
	this.hdr.reply_type = SM_RT_INVAL

	this.sequence = 0
	FOR i__ = 1 TO SM_MAX_JOINT DO
		this.joint_data[i__] = 0.0
	ENDFOR
END sm000A_rset




ROUTINE sm000A_read
VAR
	stat__       : INTEGER
BEGIN
	stat__ = 0

	-- NOTE: ONLY fields in msg body are deserialised by this routine,
	--       pkt_len & header are CALLERs reponsibility

	-- sequence + joint_data
	READ fd(this.sequence,
	        this.joint_data[ 1], this.joint_data[2], this.joint_data[3],
	        this.joint_data[ 4], this.joint_data[5], this.joint_data[6],
	        this.joint_data[ 7], this.joint_data[8], this.joint_data[9],
	        this.joint_data[10])
	stat__ = IO_STATUS(fd)
	IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF

	-- TODO: perhaps return nr of bytes consumed, so it can be checked
	--       against header.len by caller
	RETURN (-ABS(stat__))
END sm000A_read




ROUTINE sm000A_wrte
VAR
	stat__       : INTEGER
BEGIN
	stat__ = 0

	-- NOTE: ONLY fields in msg body are serialised by this routine,
	--       pkt_len & header are CALLERs reponsibility

	-- body fields
	WRITE fd(this.sequence,
	         this.joint_data[ 1], this.joint_data[2], this.joint_data[3],
	         this.joint_data[ 4], this.joint_data[5], this.joint_data[6],
	         this.joint_data[ 7], this.joint_data[8], this.joint_data[9],
	         this.joint_data[10])
	stat__ = IO_STATUS(fd)
	IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF

	-- flush write buffer
	WRITE fd(CR)
	stat__ = IO_STATUS(fd)

	RETURN (-ABS(stat__))
END sm000A_wrte




--- list routines --------------------------------------------------------------

ROUTINE sm000Al_ctor
BEGIN
	sm000Al_cler(this)
	RETURN (0)
END sm000Al_ctor




ROUTINE sm000Al_size
BEGIN
	RETURN (PATH_LEN(this))
END sm000Al_size




ROUTINE sm000Al_cler
VAR
	i__ : INTEGER
BEGIN
	i__ = sm000Al_size(this)
	-- TODO: why not just use 'i__'?
	WHILE (sm000Al_size(this) > 0) DO
		sm000Al_rmov(this, i__)
		i__ = sm000Al_size(this)
	ENDWHILE
END sm000Al_cler




ROUTINE sm000Al_add
VAR
	stat__ : INTEGER
	tmp__  : INTEGER
BEGIN
	APPEND_NODE(this, stat__)
	-- TODO: check status of operation
	tmp__ = sm000Al_size(this)
	this[tmp__] = e
END sm000Al_add




ROUTINE sm000Al_apnd
BEGIN
	sm000Al_add(this, e)
END sm000Al_apnd




ROUTINE sm000Al_set
VAR
	tmp__  : sm000A_lit_t
	stat__ : INTEGER
BEGIN
	tmp__ = sm000Al_get(this, idx)
	this[idx] = e
	RETURN (tmp__)
END sm000Al_set




ROUTINE sm000Al_get
BEGIN
	RETURN (this[idx])
END sm000Al_get




ROUTINE sm000Al_take
VAR
	tmp__  : sm000A_lit_t
	stat__ : INTEGER
BEGIN
	tmp__ = sm000Al_get(this, idx)
	DELETE_NODE(this, idx, stat__)
	-- TODO: check status of operation
	RETURN (tmp__)
END sm000Al_take




ROUTINE sm000Al_rmov
VAR
	stat__ : INTEGER
BEGIN
	DELETE_NODE(this, idx, stat__)
	-- TODO: check status of operation
END sm000Al_rmov




ROUTINE sm000Al_mpty
BEGIN
	RETURN (sm000Al_size(this) = 0)
END sm000Al_mpty




ROUTINE sm000Al_ins
VAR
	stat__ : INTEGER
BEGIN
	INSERT_NODE(this, idx, stat__)
	-- TODO: check status of operation
	this[idx] = e
END sm000Al_ins


--- /list routines -------------------------------------------------------------
