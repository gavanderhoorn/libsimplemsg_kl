
-- Copyright (c) 2016, TU Delft Robotics Institute
--
-- Licensed under the Apache License, Version 2.0 (the "License");
-- you may not use this file except in compliance with the License.
-- You may obtain a copy of the License at
--
--     http://www.apache.org/licenses/LICENSE-2.0
--
-- Unless required by applicable law or agreed to in writing, software
-- distributed under the License is distributed on an "AS IS" BASIS,
-- WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
-- See the License for the specific language governing permissions and
-- limitations under the License.


PROGRAM libsm000F
--------------------------------------------------------------------------------
--
-- ROS-Industrial Simple Message JOINT_FEEDBACK (libsm000F) implementation
--
-- author: G.A. vd. Hoorn (TU Delft Robotics Institute)
--
--------------------------------------------------------------------------------
%ALPHABETIZE
%COMMENT = 'ROS//r0'
%NOBUSYLAMP
%NOLOCKGROUP
%NOPAUSE = COMMAND + TPENABLE + ERROR




--------------------------------------------------------------------------------
--
-- remote types & constants
--
--------------------------------------------------------------------------------
%INCLUDE ktype_sz.klt
%INCLUDE simplemsg\simplemsg.klt




--------------------------------------------------------------------------------
--
-- local types & constants
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\msgs\sm000F.klt




--------------------------------------------------------------------------------
--
-- local variables
--
--------------------------------------------------------------------------------
VAR
	msg_len_     : INTEGER




--------------------------------------------------------------------------------
--
-- remote routine prototypes
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\sm_hdr.klh




--------------------------------------------------------------------------------
--
-- local routine prototypes
--
--------------------------------------------------------------------------------
%INCLUDE simplemsg\msgs\sm000F.klh




--------------------------------------------------------------------------------
--
-- Empty body
--
--------------------------------------------------------------------------------
BEGIN
END libsm000F




ROUTINE sm000F_len
BEGIN
	IF (UNINIT(msg_len_)) THEN
		msg_len_ = ( &
			-- id + valid_fields + time
			(2 * SZ_INT) + (SZ_REAL) + &
			-- positions, velocities, accelerations
			(SM_MAX_JOINT * SZ_REAL * 3))
	ENDIF
	RETURN (msg_len_)
END sm000F_len




ROUTINE sm000F_ctor
BEGIN
	sm000F_rset(this)
END sm000F_ctor




ROUTINE sm000F_rset
VAR
	i__          : INTEGER
BEGIN
	sm_hdr_rset(this.hdr)
	this.hdr.msg_type   = SM000F_ID
	this.hdr.comm_type  = SM_CT_TOPIC
	this.hdr.reply_type = SM_RT_INVAL

	this.robot_id     = SM000F_INVID
	this.valid_fields = 0
	this.tim_e        = 0.0
	-- always reset all elements in the array
	FOR i__ = 1 TO SM_MAX_JOINT DO
		this.positions[i__] = 0.0
	ENDFOR
	--FOR i__ = 1 TO SM_MAX_JOINT DO
	--	this.velocities[i__] = 0.0
	--ENDFOR
	--FOR i__ = 1 TO SM_MAX_JOINT DO
	--	this.accelrations[i__] = 0.0
	--ENDFOR
END sm000F_rset




ROUTINE sm000F_wrte
VAR
	stat__       : INTEGER
	i__          : INTEGER
BEGIN
	stat__ = 0

	-- NOTE: ONLY fields in msg body are deserialised by this routine,
	--       pkt_len & header are CALLERs reponsibility

	-- id + valid_fields + time
	WRITE fd(this.robot_id, this.valid_fields, this.tim_e)
	stat__ = IO_STATUS(fd)
	IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF

	-- positions
	FOR i__ = 1 TO SM_MAX_JOINT DO
		WRITE fd(this.positions[i__])
		stat__ = IO_STATUS(fd)
		IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF
	ENDFOR

	-- message is fixed size, so we serialise all fields, even if we don't
	-- really support them (velocities, accelerations)

	-- velocities: DUMMY values (unsupported)
	FOR i__ = 1 TO SM_MAX_JOINT DO
		WRITE fd(0.0)
		stat__ = IO_STATUS(fd)
		IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF
	ENDFOR

	-- accelerations: DUMMY values (unsupported)
	FOR i__ = 1 TO SM_MAX_JOINT DO
		WRITE fd(0.0)
		stat__ = IO_STATUS(fd)
		IF (stat__ <> 0) THEN RETURN (-stat__); ENDIF
	ENDFOR

	-- flush write buffer
	WRITE fd(CR)
	stat__ = IO_STATUS(fd)

	RETURN (-ABS(stat__))
END sm000F_wrte




--- list routines --------------------------------------------------------------

ROUTINE sm000Fl_ctor
BEGIN
	sm000Fl_cler(this)
	RETURN (0)
END sm000Fl_ctor




ROUTINE sm000Fl_size
BEGIN
	RETURN (PATH_LEN(this))
END sm000Fl_size




ROUTINE sm000Fl_cler
VAR
	i__ : INTEGER
BEGIN
	i__ = sm000Fl_size(this)
	-- TODO: why not just use 'i__'?
	WHILE (sm000Fl_size(this) > 0) DO
		sm000Fl_rmov(this, i__)
		i__ = sm000Fl_size(this)
	ENDWHILE
END sm000Fl_cler




ROUTINE sm000Fl_add
VAR
	stat__ : INTEGER
	tmp__  : INTEGER
BEGIN
	APPEND_NODE(this, stat__)
	-- TODO: check status of operation
	tmp__ = sm000Fl_size(this)
	this[tmp__] = e
END sm000Fl_add




ROUTINE sm000Fl_apnd
BEGIN
	sm000Fl_add(this, e)
END sm000Fl_apnd




ROUTINE sm000Fl_set
VAR
	tmp__  : sm000F_lit_t
	stat__ : INTEGER
BEGIN
	tmp__ = sm000Fl_get(this, idx)
	this[idx] = e
	RETURN (tmp__)
END sm000Fl_set




ROUTINE sm000Fl_get
BEGIN
	RETURN (this[idx])
END sm000Fl_get




ROUTINE sm000Fl_take
VAR
	tmp__  : sm000F_lit_t
	stat__ : INTEGER
BEGIN
	tmp__ = sm000Fl_get(this, idx)
	DELETE_NODE(this, idx, stat__)
	-- TODO: check status of operation
	RETURN (tmp__)
END sm000Fl_take




ROUTINE sm000Fl_rmov
VAR
	stat__ : INTEGER
BEGIN
	DELETE_NODE(this, idx, stat__)
	-- TODO: check status of operation
END sm000Fl_rmov




ROUTINE sm000Fl_mpty
BEGIN
	RETURN (sm000Fl_size(this) = 0)
END sm000Fl_mpty




ROUTINE sm000Fl_ins
VAR
	stat__ : INTEGER
BEGIN
	INSERT_NODE(this, idx, stat__)
	-- TODO: check status of operation
	this[idx] = e
END sm000Fl_ins


--- /list routines -------------------------------------------------------------
